# 汇编语言

> 这个学期大家将会学习到一些汇编语言相关的一些只是，今天我便给大家分享一些`80X86`相关的汇编语言的知识。希望大家能够在接下来的学习中变得轻松一些。



### 内存与存储器相关的先导知识

#### 主存储器与内存

这些装置都是通过容纳0和1对信息进行存储，每一个存放0，1的位置被称为存储位。而每八个存储位 组成了一个字节，也就是地址标号的最基础的单位。下面举一个例子，我们假设主存的容量位`2MB`，则它的最低的地址为0，那么它的最高的地址就是 `1FFFFFH` 。这个想必大家应该是可以看懂的。这里的表示是一个16进制的数。**假如是由多个字节组成了一个存储单位，例如字，双字等等，地址是由最低位的字节进行确定**



#### 堆栈

这个和数据结构中的栈在我来看是差不多的，它们都是有着一个先进后出的约束条件。在这里需要说明的是，在存储的地址表示上高地址是在栈底的。存入时栈顶均由高地址向低地址变化。`PUSH` 与 `POP` 是向堆栈中存储数据专门的指令。



#### 寄存器
一个典型的CPU是由运算器，控制器，寄存器等器件组成。不同的CPU，寄存器的个数，结构是有差别的。有几个16位的寄存器，大家还是需要去了解一下的。`AX, BX, CX, DX` 这几个16位的寄存器主要是用来存放一些一般性的数据。相应的在这些前面加一个R便是64位的寄存器，在前面加一个E便是32位寄存器。当然它们也是可以向下兼容，比如 `AX` 可以被分为 `AH` 与 `AL` 两个8位寄存器。这些知只是让大家知道一下由寄存器这个东西的存在，其实需要了解的知识还是比较多的。

 

### 寻址方式

> 值得注意的是，我在这里总结出来的一些表达方式和你们接下来将要学习的 `csapp` 上面的使用格式上面可能存在着一定的出入，主要的区别是 `csapp` 会把这部分的内容区分的更加详细。我在下面会进行一定的说明，同时将它们总结为一种更加易懂的理解方式。

#### 寄存器寻址

在这种寻址方式中，指令所指明的寄存器就是操作数的存放地址，即操作数在指令指明的寄存器中。

**使用格式**  `R`   示例  `INC BX`  这个指令的意思是对BX这个寄存器中的数值进行加一的操作。



#### 寄存器间接寻址

在寄存器间接寻址方式中，操作数存放在主存储器中，而寄存器中存放的是操作数的偏移地址。

**使用格式** `[R]` 

值得注意的是，在 `80X86` 中，这个R可以是任意一个通用的三十二位的寄存器，但是在十六位的寄存器中就受到了些许的限制，它只能够选择 `BX， DI， SI， BP` 中的一个。八位的通用寄存器是不可以使用的。

**在 `csapp` 中这种寻址方式被直接称为了间接寻址**

 

#### 变址寻址

变址寻址的操作数存放在主存储器中。

**使用格式** `[R*F+V]` 或 `[R*F]+V` 或 `V[R*F]`

在这里，表示的是寄存器R的内容乘以指定的比例因子F（1，2，4，8）后加上给定的位移量V作为操作数的偏移地址。这个具体的在`CSAPP`中表示为 `Imm(r)` 这里的`Imm` 便是表示的是V，括号中的r表示的便是寄存器R。如果需要加上比例因子的话便是 `Imm(,r,s)`

**在 `csapp` 中这种寻址方式被称为了基址加偏移量寻址与比例变址寻址**

给大家举一个例子：`4(%rax)`  假如寄存器 `rax` 中存储的值是 `0x100` 的话，这种寻址方式最后得到的结果便是 `0x104` 中所存储的值。



#### 基址加变址寻址

这种寻址方式的操作数依旧是存放在存储器中的，它的偏移地址是指令中指定的基址寄存器的内容，变址寄存器中的内容乘以比例因子与位移量三项相加的和

**使用格式** `Imm(rb,rs,s)` 在 `csapp` 中 倘若是没有比例因子，它便是被归纳在了变址寻址中，如果有了比例因子，便是被归纳在了比例变址寻址这一个类别中。这是没有比例因子的表示方式 `(rb,rs) 或者 Imm(rb,rs)`  



#### 立即寻址 

这个的使用方式是比较简单的，是直接将操作数给体现出来了

**使用格式** `n` 在 `csapp` 中的表示方式是 `$Imm` 



#### 直接寻址 && 绝对寻址

**使用格式** `Imm` 这种表示的方式是直接在主存储器中找这个偏移地址所表示的内容。



#### 下面我们做点题目试一下

1. 分别指出下列指令中源操作数和目的操作数的寻址方式。 **这里选择题目是 `80X86` 的格式**

```
(1)MOV SI, 10   
(2)MOV DI, [EAX]  
(3)ADD EAX, 4[BX]  
(4)SUB DX, 5[BX +DI]  
(5)MOV [EDI*4 +2], AX   
(6)MOV BH, DS:[10]  
(7)ADD [SI], SI  
(8)ADD CX, -7[BP +DI]  
```



### 常用的机器指令语句

最频繁使用的指令是将数据从一个位置复制到另一个位置的指令。

`MOV` 类便是最简单形式的数据传送指令。它主要由四条指令组成 `movb, movw, movl, movq` 这里表示的意思是传送字节，传送字，传送双字，传送四字。在不同格式的汇编语言中，有的是把前面作为目的地址，有的是将后面作为目的地址。这里我们进行介绍的主要是把后面作为目的地址，前面作为源操作数地址。

值得注意的两个地址不能全都是内存中的地址。这里就说明了假如我们想要把一个内存中的内容复制到内存中的另一个位置，我们就不能直接使用一个 `mov` 去实现，我们需要分为两步，先将源操作数取出放入到一个寄存器中，然后再通过这个寄存器将操作数放入到目的地址中。



下面我们结合一下今天的内容看一段内容

指出程序执行之后, 以`BUF2、BUF3、BUF4` 为首址的3 个字节存储区中存放的数据。

注意：在这里目的地址放在了前面。

```
.386
STACK   SEGMENT  USE16 STACK
        DB  200 DUP(0)
STACK   ENDS
DATA    SEGMENT USE16
BUF1    DB 0, 1, 2, 3, 4, 5, 6, 7, 8, 9
BUF2    DB 10 DUP(0)
BUF3    DB 10 DUP(0)
BUF4    DB 10 DUP(0)
DATA    ENDS
CODE    SEGMENT USE16
        ASSUME CS: CODE, DS: DATA, SS: STACK
START:  MOV AX, DATA
        MOV DS, AX
        MOV SI, OFFSET BUF1
        MOV DI, OFFSET BUF2
        MOV BX, OFFSET BUF3
        MOV BP, OFFSET BUF4
        MOV CX, 10
LOPA:   MOV AL, [SI]
        MOV [DI], AL
        INC AL
        MOV [BX], AL
        ADD AL, 3
        MOV DS:[BP], AL
        INC SI
        INC DI
        INC BP
        INC BX
        DEC CX
        JNZ LOPA
        MOV AH, 4CH
        INT 21H
CODE    ENDS
        END START
```



```
.386
STACK   SEGMENT  USE16 STACK
        DB  200 DUP(0)
STACK   ENDS
DATA    SEGMENT USE16
BUF1    DB 0, 1, 2, 3, 4, 5, 6, 7, 8, 9
BUF2    DB 10 DUP(0)
BUF3    DB 10 DUP(0)
BUF4    DB 10 DUP(0)
DATA    ENDS
CODE    SEGMENT USE16
        ASSUME CS: CODE, DS: DATA, SS: STACK
START:  MOV AX, DATA    ;这两句完成将数据段首址DATA置入数据段寄存器DS中
        MOV DS, AX
        MOV SI, OFFSET BUF1     ;OFFSET为取偏移地址算符
        MOV DI, OFFSET BUF2     ;此四句为取BUF1……BUF4的偏移地址送入寄存器之中
        MOV BX, OFFSET BUF3
        MOV BP, OFFSET BUF4
        MOV CX, 10      ;将立即数10送入CX寄存器中，作接下来作循环操作时的计数量，起到类似于高级语言（例如C语言）中for循环中i的作用
LOPA:   MOV AL, [SI]    ;此句用了寄存器间接寻址的方法，将SI中所存地址对应的内存中的数据送入寄存器AL之中
        MOV [DI], AL    ;此句将AL之中的数据，送入DI对应的的偏移地址的内存单元中
        INC AL          ;AL自增
        MOV [BX], AL    ;将自增后的AL中存储的数送入送入BX对应的的偏移地址的内存单元中
        ADD AL, 3       ;将AL中的数据再加上3，然后存储在AL中
        MOV DS:[BP], AL ;将AL中存储的数送入BP对应的的偏移地址的内存单元中
        INC SI          ;此四句将SI、DI、BP、CX分别自增，指向各自其后面的一个内存存储单元
        INC DI
        INC BP
        INC BX
        DEC CX          ;对CX自减
        JNZ LOPA        ;当CX不等于零时跳转到LOPA标号对应的地方继续执行指令
        MOV AH, 4CH     ;此两句为DOS功能调用指令，执行完该指令以后，计算机将结束本程序的运行，返回DOS状态
        INT 21H
CODE    ENDS
        END START
```



### 总结一下

在下面一个阶段中，大家阅读 `csapp` 的首个部分便是汇编语言的相关知识，在这部分的章节中，大家会对汇编语言深入的学习，今天知识给大家分享了一点和寻址方式相关的内容，这个是看懂指令最为基础的一部分。这里面还有许多细节上的问题，希望大家能够自己在阅读的过程中去发现。













 

